#!/usr/bin/env perl

use warnings;
use strict;

use File::Spec::Functions;
use Cwd qw(abs_path);

{ package RCFiles;
    # import subs we just loaded from the main namespace
    *abs_path = *::abs_path;
    *catfile  = *::catfile;

    our (@symlinks)  = qw(bashrc gitconfig psqlrc tmux.conf vimrc zshrc);
    our (%settings)  = (
        'alias.full-diff'         => '!$(git root)/setup/git-full-diff.sh',
        'alias.ls-untracked'      => 'ls-files -o --exclude-standard',
        'alias.is-dirty'          =>
            '![ $(git status --porcelain | wc -l) -ne 0 ]',
        'filter.gitconfig.clean'  =>
            '$(git root)/setup/strip-ini-section.pl %f user',
        'filter.gitconfig.smudge' => 'cat',
    );

    predeclared_subs: {
        sub __chdir__(;$);
        sub __success__() { 0 }
        sub __failure__() { 1 }
    }

    sub relink {
        foreach my $symlink (@symlinks) {
            my ($sympath)  = catfile($ENV{HOME}, ".$symlink");
            my ($realpath) = catfile($ENV{RCDIR}, 'configs', $symlink);
            my ($resolved) = abs_path($sympath);

            next if ($resolved eq $realpath);

            if (-e $resolved) {
                print "=> Moving $resolved to $realpath\n";
                rename($resolved, $realpath) if (-e $resolved);
            }

            print "=> Linking $sympath to $realpath\n";
            symlink($realpath, $sympath);
        }

        __success__
    }

    sub autoconfig {
        my ($changed);
        my ($cwd) = `pwd`;
        chomp($cwd);

        __chdir__ or return __failure__;

        while (my ($setting, $value) = each(%settings)) {
            my ($curval) = `git config $setting` || '';
            chomp($curval);

            if ($curval ne $value) {
                $changed = 1;

                $ENV{VALUE} = $value;
                `git config $setting "\$VALUE"`;

                if ($?) { warn "Could not set $setting to $value\n" }
                else    { print "=> set $setting to $value\n" }
            }
        }

        # since nothing else would happen when this is called, let people know
        # the command was successful.
        print "=> Existing configuration is valid\n" unless $changed;

        warn "Couldn't return to working directory $cwd\n" unless chdir($cwd);
        __success__
    }

    sub status {
        __chdir__ or return __failure__;

        `git is-dirty 2>&1 1>/dev/null`;
        if ($?) {
            print "=> RC Files are locally unmodified\n";
            __success__
        }
        else {
            print "=> RC Files have been locally modified\n=>\n";
            print "=> To view differences, run:\n=>   rcfiles diff\n=>\n";
            print "=> To save changes, run:\n=>   rcfiles save\n";

            __failure__
        }
    }

    sub diff {
        __chdir__ or return __failure__;
        system(qw(git full-diff)) ? __failure__ : __success__
    }

    sub save {
        if (update() == __failure__) {
            warn "fatal: Will not save without updating\n";
            return __failure__;
        }

        __chdir__ or return __failure__;
        my (@untracked) = `git ls-untracked 2>&1`;
        my ($exit) = sub {
            foreach my $untracked (@untracked) {
                chomp($untracked);
                $ENV{TO_REMOVE} = $untracked;
                `git reset HEAD "\$TO_REMOVE"`;
            }
            shift;
        };

        foreach my $untracked (@untracked) {
            chomp($untracked);

            $ENV{TO_ADD} = $untracked;
            `git add "\$TO_ADD" 2>&1`;

            if ($?) {
                warn "fatal: Could not add untracked $untracked\n";
                return $exit->(__failure__);
            }
        }

        system(qw(git commit -a));
        if ($?) {
            warn "fatal: Could not complete commit\n";
            return $exit->(__failure__);
        }

        `git push 2>&1`;
        if ($?) {
            warn "fatal: Could not submit updates to remote 'origin'\n";
            __failure__
        }
        else {
            print "=> Submitted changes to remote 'origin'\n";
            __success__
        }
    }

    sub update {
        my ($output, $changed);
        __chdir__ or return __failure__;

        $output  = `git fetch origin 2>&1`;
        if ($?) {
            warn "Could not synchronize with remote repository, aborting.\n";
            return __failure__;
        }
        elsif ($output) {
            print "=> Synchronized new changes\n";
            $changed = 1;
        }

        $output = `git merge --ff-only master origin/master 2>&1`;
        if ($?) {
            warn "Could not merge with remote changes, aborting.\n";
            return __failure__;
        }
        elsif ($output !~ /Already up-to-date\./) {
            print "=> Merged new changes from 'origin'\n";
            $changed = 1;
        }

        if ($changed) {
            autoconfig();
            relink();
        }
        else {
            print "=> No new updates\n" unless $changed;
        }
        __success__
    }

    sub __chdir__(;$) {
        my ($nonfatal) = shift;

        unless (chdir($ENV{RCDIR})) {
            my ($errmsg) = "Could not switch to $ENV{RCDIR}\n";
            $errmsg = "fatal: $errmsg" unless $nonfatal;

            warn $errmsg;
            return;
        }

        return 1;
    }

    sub __findsub__ {
        no strict 'refs';
        *{"*@{[ __PACKAGE__ ]}::@{[ shift ]}"}{CODE};
    }

    sub __main__ {
        my ($command) = shift;
        exit __failure__ unless $command;

        my ($subref) = __findsub__($command);
        exit __failure__ unless $subref;

        exit($subref->() || __success__);
    }
}

RCFiles::__main__(@ARGV) if ($0 eq __FILE__);
